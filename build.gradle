plugins {
    id 'net.neoforged.gradle.userdev' version '7.0.142'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    mavenLocal()
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    mavenCentral()
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${project.neoforge_version}"

    // HOCON Configuration Library
    implementation 'com.typesafe:config:1.4.3'

    // JDBC drivers
    //implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.1'
    //implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
    //implementation 'org.postgresql:postgresql:42.7.4'

    // Javalin Web Framework
    implementation 'io.javalin:javalin:6.4.0'
    implementation 'org.eclipse.jetty:jetty-server:11.0.24'
    implementation 'org.eclipse.jetty:jetty-webapp:11.0.24'
    implementation 'org.eclipse.jetty.toolchain:jetty-jakarta-servlet-api:5.0.2'
    implementation 'org.eclipse.jetty:jetty-util:11.0.24'
    implementation 'org.eclipse.jetty:jetty-http:11.0.24'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.24'
    implementation 'org.eclipse.jetty:jetty-security:11.0.24'
    implementation 'org.eclipse.jetty:jetty-io:11.0.24'
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neoforge_version    : neoforge_version,
            neoforge_version_range: neoforge_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id,
            mod_name            : mod_name,
            mod_license         : mod_license,
            mod_version         : mod_version,
            mod_authors         : mod_authors,
            mod_description     : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/octocat/hello-world'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}